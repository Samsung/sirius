--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _N-iakAicEea7hcZ9Jto1HQ,_Sc0HQAidEea7hcZ9Jto1HQ,_ISVO4AheEea7hcZ9Jto1HQ,_CxvK8AicEea7hcZ9Jto1HQ,_1dc_gAidEea7hcZ9Jto1HQ,_vnVkEENeEeagKLHJ7g9jQA,_ZuJ8AAifEea7hcZ9Jto1HQ
Element-Name: ValidationQuickFix
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _jxzdgEN_EeagKLHJ7g9jQA
Runtime-Version: 2.1.0.201606221726
Save-Time: 7/6/16 3:57 PM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

This test aims to check the quick-fix mechanism.

The test :
* loads the tests projects released with the Sirius test product,
* opens the test diagram,
* selects the validation viewpoint,
* selects an element in the diagram,
* clicks on the validation error link,
* checks the quick fix window.
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// Select the text error
get-view Properties | get-link "14 errors detected" | click
with [get-window "Quick Fix"] {
    get-table | select "Text error message..."
}
// Check that the next button is activated
get-window "Quick Fix" | get-button "Next >" | get-property enablement | equals true | verify-true
// Click to the next page
get-window "Quick Fix" | get-button "Next >" | click
// Check that it exists two quick fixes
with [get-window "Quick Fix" | get-table] {
    get-property "getItems().TableItem[0].getText()" | equals Fix1 | verify-true
    get-property "getItems().TableItem[1].getText()" | equals Fix2 | verify-true
}
// Select the quick fix
with [get-window "Quick Fix"] {
    get-table | select Fix2
    get-button Finish | click
}
// Check the name has changed in the diagram
get-editor RootDiagram | get-diagram -index 1 | get-edit-part -name RootDiagram | get-edit-part -name QuickFix2 
    | get-edit-part -className SquareEditPart | get-figure "0/0" | get-property "getText()" | equals QuickFix2 
    | verify-true
// Check the name has changed in the diagram
get-view Properties | get-section Validation | get-editbox -index 1 | get-property "getText()" | equals QuickFix2 
    | verify-true
   
// Check that the error decorator is not anymore on the widget
with [get-view Properties] {
    get-section Validation | get-editbox -index 5 | get-property "decorators[0].visible" | equals false | verify-true
    get-link "13 errors detected" | get-property caption | equals "13 errors detected" | verify-true
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
